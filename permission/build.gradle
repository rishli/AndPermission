apply plugin: rootProject.ext.plugins.library

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
    }

    resourcePrefix 'permission'

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
}

static def releaseDate() {
    return new Date().format("yyyyMMdd")
}

afterEvaluate {
    // for app test, copy the final aar to application libs
    android.libraryVariants.all { variant ->
        File outputFile = variant.outputs.first().outputFile
        tasks.named("assemble${variant.name.capitalize()}").configure {
            doLast {
                copy {
                    from outputFile
                    into "aar_output"
                    def releaseDate = releaseDate()
                    def versionName = rootProject.ext.android.versionName
                    rename outputFile.name, "AndPermission-${releaseDate}-v${versionName}-without-androidx.aar"
                }
            }
        }
    }
}

dependencies {
    api rootProject.ext.dependencies.fragment
}